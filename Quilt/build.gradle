plugins {
	id 'org.quiltmc.loom' version '0.12.+'
	id 'maven-publish'
	id 'com.matthewprenger.cursegradle' version '1.4.0'
	id "com.modrinth.minotaur" version "1.2.1"
}

archivesBaseName = "${mod_id}-quilt-${minecraft_version}"

repositories {

	maven {
		name 'Quilt (Release)'
		url 'https://maven.quiltmc.org/repository/release/'
	}

	maven {
		name 'Quilt (Snapshot)'
		url 'https://maven.quiltmc.org/repository/snapshot/'
		content {
			includeGroup 'org.quiltmc.fabric_api_qsl'
			includeGroupByRegex 'org\\.quiltmc\\.qsl(?:\\.[a-z_]+)?'
		}
	}
	maven {
		name = "TerraformersMC"
		url = "https://maven.terraformersmc.com/"
	}
}

configurations {
	testCompileOnly.extendsFrom(compileOnly)
}

dependencies {

	minecraft "com.mojang:minecraft:${minecraft_version}"
	mappings loom.layered() {
		officialMojangMappings()
		parchment("org.parchmentmc.data:parchment-${parchment_minecraft_version}:${parchment_version}@zip")
	}
	modImplementation "org.quiltmc:quilt-loader:${quilt_loader_version}"

	modImplementation("org.quiltmc:qsl:${quilt_stdlib_version}") {
		exclude group: "net.fabricmc"
	}

	// Needed for Hyle, etc.
	modImplementation("org.quiltmc.quilted-fabric-api:quilted-fabric-api:${quilted_fabric_api}") {
		exclude group: "org.quiltmc", module:" qsl"
	}

	implementation project(":Common")

	compileOnly group:'com.google.auto.service', name:'auto-service', version:'1.0.1'
	annotationProcessor group:'com.google.auto.service', name:'auto-service', version:'1.0.1'

	modCompileOnly "curse.maven:hyle-609850:3746990"

	compileOnly("me.shedaniel:RoughlyEnoughItems-api:${rootProject.rei_version}"){transitive=false}
	//modRuntimeOnly("me.shedaniel:RoughlyEnoughItems-fabric:${rootProject.rei_version}")

	modCompileOnly("mezz.jei:jei-${rootProject.jei_minecraft_version}-common-api:${rootProject.jei_version}")
	modCompileOnly("mezz.jei:jei-${rootProject.jei_minecraft_version}-fabric-api:${rootProject.jei_version}")

	modImplementation("io.github.lukebemish.dynamic_asset_generator:dynamic_asset_generator-quilt-${rootProject.minecraft_version}:${rootProject.dynassetgen_version}")
	modImplementation("io.github.lukebemish.defaultresources:defaultresources-quilt-${rootProject.minecraft_version}:${rootProject.defaultresources_version}")
	modImplementation("io.github.lukebemish.codecutils:codecutils-quilt-${rootProject.minecraft_version}:${rootProject.codecutils_version}")

	modImplementation "dev.emi:emi:${emi_version}"

	include("io.github.lukebemish.defaultresources:defaultresources-quilt-${rootProject.minecraft_version}:${rootProject.defaultresources_version}")
	include("io.github.lukebemish.codecutils:codecutils-quilt-${rootProject.minecraft_version}:${rootProject.codecutils_version}")
}

loom {
	accessWidenerPath = file("src/main/resources/excavated_variants.accessWidener")

	runs {
		client {
			client()
			setConfigName("Quilt Client")
			ideConfigGenerated(true)
			runDir("run")
		}
		server {
			server()
			setConfigName("Quilt Server")
			ideConfigGenerated(true)
			runDir("runserver")
		}
	}

	mixin {
		defaultRefmapName = "excavated_variants.refmap.json"
	}
}

processResources {
    from project(":Common").sourceSets.main.resources
	inputs.property "version", project.version

	filesMatching("quilt.mod.json") {
		expand "version": project.version
	}
}

tasks.withType(JavaCompile) {
    source(project(":Common").sourceSets.main.allSource)
}

jar {
	from("LICENSE") {
		rename { "${it}_${mod_name}"}
	}
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId project.group
            artifactId project.archivesBaseName
            version project.version
            from components.java
        }
    }

    repositories {
    }
}

// Publishing

def gitBranch() {
	def branch = ""
	def proc = "git rev-parse --abbrev-ref HEAD".execute()
	proc.in.eachLine { line -> branch = line }
	proc.err.eachLine { line -> println line }
	proc.waitFor()
	branch
}

curseforge {
	if (System.getenv('CURSEFORGE_KEY') != null) {
		apiKey = System.getenv('CURSEFORGE_KEY')
		project {
			id = '577411'
			changelog = "See: https://github.com/lukebemish/excavated_variants/blob/${gitBranch()}/changelogs/CHANGELOG.md"
			releaseType = "${project.cf_version_type}"
			addGameVersion "${project.minecraft_version}"
			addGameVersion 'Quilt'
			mainArtifact(remapJar) {
				displayName = "Excavated Variants [Quilt] v$project.version"
			}
			relations {
				requiredDependency 'dynamic-asset-generator'
			}
		}
	}
	options {
		forgeGradleIntegration = false
	}
}

import com.modrinth.minotaur.TaskModrinthUpload

task modrinth (type: TaskModrinthUpload){ // Make sure it runs after build!
	onlyIf {
		System.getenv("MODRINTH_KEY") // Only attempt to run this task if the MODRINTH variable is set, otherwise SKIP it
	}

	token = System.getenv("MODRINTH_KEY") // An environment property called MODRINTH that is your token, set via Gradle CLI, GitHub Actions, Idea Run Configuration, or other
	projectId = 'IZaMGxb0'
	versionNumber = "$project.version-$project.minecraft_version-quilt" // Will fail if Modrinth has this version already
	// On fabric, use 'remapJar' instead of 'jar'
	versionType = "${project.modrinth_version_type}"
	detectLoaders = false

	uploadFile = remapJar // This is the java jar task. If it can't find the jar, try 'jar.outputs.getFiles().asPath' in place of 'jar'
	addGameVersion("${project.minecraft_version}") // Call this multiple times to add multiple game versions. There are tools that can help you generate the list of versions
	addLoader('quilt')
}

afterEvaluate {
	tasks.curseforge.dependsOn build
	tasks.modrinth.dependsOn build
}
